plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group 'com.chilikinov'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

allure {
    version.set("2.19.0")
    adapter { //отвечает за прявление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set("2.19.0") //варсия интеграции фреймворка и Allure
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {

    testImplementation(
            'org.slf4j:slf4j-simple:2.0.6'
            ,'io.rest-assured:rest-assured:5.3.0'
            ,'io.rest-assured:rest-assured-common:5.0.1'
            ,'io.rest-assured:json-path:5.0.1'
            ,'io.rest-assured:xml-path:5.0.1'
            ,'org.junit.jupiter:junit-jupiter:5.9.2'
            ,'org.assertj:assertj-core:3.11.1'
            ,'org.hamcrest:hamcrest-all:1.3'
            ,'com.codeborne:selenide:6.11.1'
            ,'org.selenide:selenide-selenoid:2.3.4'
            ,'io.qameta.allure:allure-selenide:2.19.0'
            ,'com.google.code.gson:gson:2.2.4'
            ,'org.aeonbits.owner:owner:1.0.12'
            ,'com.github.javafaker:javafaker:1.0.2'
            ,'com.google.code.gson:gson:2.2.4'

    )

    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

tasks.withType(Copy).all {
    duplicatesStrategy 'exclude'
}

task regression_test(type: Test) {
    useJUnitPlatform {
        includeTags("git_repo")
        includeTags("api_reqres_in")
    }
}

